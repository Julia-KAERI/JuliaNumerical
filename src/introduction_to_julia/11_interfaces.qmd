---
title: "인터페이스"

number-sections: true
number-depth: 2
crossref:
  chapters: false
---

반복자(Iterator) 는 컨테이너 타입으로 포함하는 성분을 처음부터 끝까지 하나 하나 접근 할 수 있는 자료구조를 말하며, Julia 에서 제공하는 컨테이너 타입은 대부분 iterator 입니다. 배열이나 튜플과 같이 순서가 정해져 있을 수도 있고 사전과 같이 순서가 임의적일 수는 있지만 어쨌든 하나하나 접근 할 수 있어야 합니다. 다음과 같은 자료형은 정의상 4개의 값을 저장할 수 있는 컨테이너이지만 반복자는 아닙니다.

```julia
struct NonIterator
    first
    second
    third
    final
end
```

Julia 에서 어떤 타입이 반복자라는 것은 그 타입에 대해 아래의 메서드가 정의되어 있다는 것을 말합니다. 아래에서 `state` 는 컨테이너의 아이템과 1대1 대응되는 어떤 값이며 배열의 경우는 인덱스입니다.

| 메서드 | 설명 |
|:-----:|:----------------|
| `iterate(iter)` |	컨테이너의 첫번째 아이템과 상태를 튜플로 반환합니다. 빈 컨테이너의 경우는 `nothing` 을 반환합니다. |
| `iterate(iter, state)` | `state` 에 의히 지정되는 아이템의 다음 아이템과 그 `state` 를 튜플로 반환합니다. 마지막 값일 경우는 `nothing` 을 반환합니다. |

</br>
반복자에 대해 추가로 정의 할 수 있는 메서드에는 다음과 같은 것이 있습니다. 

| 선택적 메서드 | 기본 정의 | 설명 |
|:--------:|:---------:|:-------------------|
| `Base.IteratorSize(IterType)` | `Base.HasLength()` | 길이를 가지면 `Base.HasLength()`, 차원을 가지면 `Base.HasShape{N}()`, 무한개의 사이즈이면 `Base.IsInfinite()`, 크기를 특정할 수 없으면 `Base.SizeUnknown()` 을 반환합니다. |
| `Base.IteratorEltype(IterType)` | `Base.HasEltype()` | `eltype(Itertype)` 이 존재하면 `Base.EltypeUnknown()`, 아니면 `Base.HasEltype()` 을 반환합니다.|
| `eltype(IterType)` | Any | `iterate()` 에 의해 반환되는 첫번째 성분의 타입을 반환합니다. |
| `length(iter)` | (undefined) | 아이템 갯수 |
| `size(iter, [dim])` | (undefined) | 각 차원에서의 아이템 갯수 |


</br>

## 인덱스로 접근가능한 반복자 

이제 유용한 반복자를 하나 만들어 봅시다. 신호처리에서 많이 사용하는 베열가운데 로그스케일로 변화하는 배열이 있습니다. matlab 에서의 `logspace` 함수, python 에서의 `numpy.logspace` 함수로 `logspace(a, b, n, base=10)` 는  `base` 의 `a` 승부터 `base` 의 `b` 승까지 로그스케일로 변하는 배열입니다. Julia 에서는 `base .^ range(a,b,n)` 과 그 결과가 같습니다.(정확히 말하면 끝값을 포함 하느냐 하지 않느냐에 따라 다릅니다. Matlab 과 julia 는 포함하며 python 은 포함하지 않습니다.)

이 반복자를 `LogSpacingRange` 라고 합시다. 우리의 목표는 `logspace` 함수로 `LogSpacingRange` 객체 생성시켜 배열처럼 사용 할 수 있게 하는 것입니다. 배열이나 튜플처럼 인덱스로 접근 가능한 객체를 만들기 위해서는 위해서는 다음의 네가지 메서드를 정의해 줘야 합니다.

| 메서드 | 설명 |
|:-----:|:---------------|
| `getindex(X, i)` | `X[i]`, 인덱스를 이용한 접근 |
| `setindex!(X, v, i)` | `X[i] = v`, 인덱스를 이용한 할당 |
| `firstindex(X)` | `X[begin]` 로 접근 가능한 첫번째 성분 |
| `lastindex(X)` | `X[end]` 로 접근 가능한 마지막 성분 |

`LogSpacingRange` 객체는 immutable 인 것이 좋을 것 같습니다. 이 경우 `LogSpacingRange` 타입에 대한 `setindex` 메서드를 작성하지 않으면 됩니다. 나머지 함수에 대해서는 적절하게 만들어 줍니다. 전체 코드는 다음과 같습니다.


```julia
struct LogSpacingRange{T}
    start::T
    stop::T
    base::T
    size::Int64

    function LogSpacingRange(start::Real, stop::Real, size::Int64, base::Real)
        @assert base > 1
        @assert size ≥ 1
        ftype = promote_type(typeof(start), typeof(stop))
        if ~(ftype <: AbstractFloat)
            ftype = Float64
        end

        return new{ftype}(start, stop, base, size)
    end
end

Base.size(p::LogSpacingRange) = (p.size, )
Base.length(p::LogSpacingRange) = p.size
Base.iterate(p::LogSpacingRange, state=1) = state > p.size ? nothing : (p[state], state+1)
Base.eltype(p::LogSpacingRange{T}) where {T} = T
Base.IteratorSize(::LogSpacingRange{T}) where {T} = Base.HasLength()
Base.IteratorEltype(::LogSpacingRange{T}) where {T} = Base.HasEltype()

function Base.getindex(p::LogSpacingRange, i::Int64) 
    @assert 0 < i ≤ p.size
    if p.size == 1
        return p.start
    else 
        r = p.start + (p.stop-p.start)/(p.size-1)*(i-1)
        return (p.base)^r
    end
end

function logspace(a, b, n::Integer=10, base::Real=10)
    return LogSpacingRange(a, b, n, base)
end
```

</br>

`logspace(a, b, n=10, base=10)` 함수를 통해 `LogSpacingRange` 타입의 반복자를 생성 할 수 있습니다. 

```julia
for v in logspace(1, 5, 3, 10)
    @show v
end 
```

를 통해 다음과 같은 결과를 얻습니다. 

```txt
v = 10.0
v = 1000.0
v = 100000.0
```

또한 `getindex` 함수를 정의하여 배열처럼 사용 할 수 있습니다. 

```txt
In [6]: a=logspace(1, 10, 10)
Out[6]: NAJ.LogSpacingRange{Float64}(1.0, 10.0, 10.0, 10)

In [7]: a[3]
Out[7]: 1000.0
``` 

물론 `10.0.^range(1, 5, length=3)` 이 같은 기능을 하는 배열을 만들어 줍니다. `LogSpacingRange` 의 장점은 숫자 세개만으로 큰 크기의 배열의 기능을 수행 할 수 있다는 것입니다. 물론 `collect()` 함수를 통해 배열로 변경하는 경우 그 장점은 사라지지만 말입니다.


</br>

## 배열처럼 동작하는 반복자

앞서의 `LogSpacingRange` 는 1차원 배열, 혹은 튜플처럼 동작했습니다. 그렇다면 배열, 특히 다차원 배열과 같이 동작하기 위해서는 다음의 메서드가 정의되어야 합니다.

| 메서드 | 설명  |
|:-----------:|:------------------|
| `size(A)` | `A` 의 차원 튜플 |
| `getindex(A, i::Int)` | 선형 인덱스로의 접근 |
| `getindex(A, I::Vararg{Int, N})` | 데카르트 인덱스로의 접근 |


</br>

다차원 배열처럼 동작하는 반복자를 만들어 봅시다. Julia 에서 많이 사용되는 패키지 가운데 [`OffsetArrays.jl`](https://github.com/JuliaArrays/OffsetArrays.jl) 이 있습니다. `OffsetArrays` 는 인덱스가 1 이 아닌 임의의 정수부터 시작하는 배열을 만들 수 있습니다. 그것은 `Base.getindex` 나 `Base.setindex!` 등을 다시 정의하여 동작하게 합니다. `OffsetArrays` 는 상당히 진지한, 그러니까 심각한 작업에도 사용할 수 있도록 세심하게 만들어진 타입이지만 우리는 여기어 이와 비슷하게 작동하는, 그리고 교육 목적의 타입을 만들어 봅시다. 이름은 `ShiftedIndexedArray` 로 합니다.

```julia
struct ShiftedIndexedArray{T, N, AA <: AbstractArray{T, N}} 
    A::AA
    offsets::NTuple{N, Int}
    
    function ShiftedIndexedArray{T, N, AA}(arr::AA, offsets::NTuple{N, Int}) where {T, N, AA<:AbstractArray{T, N}}
        return new{T, N, AA}(arr, offsets)
    end
end

function ShiftedIndexedArray(arr::AA, offsets::NTuple{N, Int}) where {N, AA<:AbstractArray}
    return ShiftedIndexedArray{eltype(arr), ndims(arr), typeof(arr)}(arr, offsets)
end

const ShiftedIndexedVector{T, AA<: AbstractVector{T}} = ShiftedIndexedArray{T, 1, AA}
const ShiftedIndexedMarix{T, AA<:AbstractMatrix{T}} = ShiftedIndexedArray{T, 2, AA}

function Base.getindex(S::ShiftedIndexedArray{T, N, AA}, I...) where {T, N, AA}
    oind = _original_cartesian_index(S, I)
    if checkbounds(Bool, S.A, oind...) 
        return getindex(S.A, oind...)
    else
        throw(BoundsError(S, I...))
    end
end

function Base.getindex(S::ShiftedIndexedArray{T, N, AA}, i::Int64) where {T, N, AA}
    oind = _original_linear_index(S, i)
    if checkbounds(Bool, S.A, oind)
        return getindex(S.A, oind)
    else 
        throw(BoundsError(S, i))
    end
end

function Base.setindex!(S::ShiftedIndexedArray{T, N, AA}, v, I... ) where {T, N, AA}
    oind = _original_cartesian_index(S, I)
    if checkbounds(Bool, S.A, oind...) == false
        throw(BoundsError(S, I...))
    else
        return setindex!(S.A, v, oind...)
    end
end

function Base.setindex!(S::ShiftedIndexedArray{T, N, AA}, v, i::Int ) where {T, N, AA}
    oind = _original_liear_index(S, i)
    if checkbounds(Bool, S.A, oind) == false
        throw(BoundsError(S, i))
    else
        return setindex!(S.A, v, oind)
    end
end


Base.similar(S::ShiftedIndexedArray) = ShiftedIndexedArray(Base.similar(S.A), S.offsets)
Base.sizeof(S::ShiftedIndexedArray) = sizeof(S.A)

Base.iterate(S::ShiftedIndexedArray, state=1) = iterate(S.A, state)
Base.IndexStyle(S::ShiftedIndexedArray) = IndexStyle(S.A)
```

`ShiftedIndexedArray` 는 두개의 필드를 갖습니다. 하나는 배열을 저장하는 `.A` 필드이고, 하나는 각 축에 대한 시작 인덱스를 지정하는 `.offsets` 입니다. 







