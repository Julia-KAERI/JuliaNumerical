# Interude : 정렬 {.unnumbered}

정렬(sorting)이란 데이터를 섞어 순서에 따라 나열하는 것을 말한다. 예를 들어 $1, 3, 2$ 를 오름차순으로 정렬하면 $1, 2, 3$ 이 되고, 내림차순으로 정렬하면 $3, 2, 1$ 이 된다. 일반적으로 정렬을 말할 때는 오름차순 정렬을 의미한다.

정렬은 컴퓨터로 아주 많이 수행하는 연산중의 하나이며, 정렬을 효율적으로 하기 위해 많은 알고리즘이 제안되었다. 여기서는 그 알고리즘을 다루지는 않으며, 기본적인 정렬 함수와 그 사용법에 대해서만 다루도록 한다. 좀 더 자세히 알고 싶으면 julia 문서 [Soring and Related Functions](https://docs.julialang.org/en/v1/base/sort/) 를 참고하라.

함수 `sort()` 가 정렬을 수행하는 기본적인 함수이다. `sort(a)` 는 컨테이너 `a` 를 정렬한 컨테이너를 반환하며 `a` 자체를 바꾸지는 않는다. 그러나 `sort!(a)` 는 `a` 를 정렬된 상태로 바꾼다. 인자에 `rev = true` 를 입력하면 내림차순으로 정렬한다.

```julia
In [1]: a=[1, 3, 5, 2]; b = sort(a)
Out[1]: 4-element Vector{Int64}:
 1
 2
 3
 5

In [2]: sort!(b, rev = true);

In [3]: b
Out[3]: 4-element Vector{Int64}:
 5
 3
 2
 1
```

컨테이너를 정렬하지 않고 정렬되는 상태의 인덱스를 반환하는 함수는 `sortperm()` 이다. 예를 들어 `v=[1, 3, 2]` 일 때 `v` 를 정렬하면 `v[1]`, `v[3], `v[2]` 로 인덱스가 `[1, 3, 2]` 순서이다. `sortperm` 은 정렬된 상태의 인덱스를 반환한다.

```julia
In [5]: sortperm([1, 3, 2])
Out[5]: 3-element Vector{Int64}:
 1
 3
 2
```

배열 `a` 에 대해 `a[[1, 3, 2, 4]]` 는 `[a[1], a[3], a[2], a[4]]` 와 같다. 즉 `a[sortperm(a)]` 는 `sort(a)` 와 같다. 이것은 어떤 컨테이너를 정럴한 순서로 다른 컨테이너를 정렬 할 수 있게 해 주기 때문이다. 예를 들어 우리가 다음과 같은 데이터를 얻었다고 하자.

| `x` | `y`|
|:----:|:----:|
| 5 | 10 |
| 2 | 3 |
| 1 | 6 |
| 7 | 12 |

배열 `x` 는 `[5, 2, 1, 7]` 이고 `y` 는 `[10, 3, 6, 12]` 이다. 이것을 보기 좋게 `x` 의 순서대로 `y` 까지 정렬해보자.

```julia
In [10]: x=[5, 2, 1, 7];y=[10, 3, 6, 12];

In [11]: v = sortperm(x); x1 = x[v]; y1 = y[v]
Out[11]: 4-element Vector{Int64}:
  6
  3
 10
 12

In [12]: for (x, y) in zip(x1, y1)
         @show x, y
         end
(x, y) = (1, 6)
(x, y) = (2, 3)
(x, y) = (5, 10)
(x, y) = (7, 12)
```

`x1` 과 `y1` 이 각각 `x` 의 순서대로 `x` 와 `y` 를 정렬한 것임을 확인 할 수 있다.


</br>

### 함수에 의한 정렬

함수 `f` 에 대해 `b = sort(a, by = f)` 는 배열 `f.(b)` 가 `f.(a)` 를 내림차순으로 정렬한 결과와 같도록 하는 `b` 를 정렬한다. 즉 `b` 는 함수 `f` 의 결과값에 따라 `a` 를 정렬한 배열이다. 다음 예를 보자. `sort(0, by=cos)` 은 `a[perm(cos.(0))]` 와 같다. 물룐 `rev=true` 를 인자로 넣으면 역순으로 정렬한다.

```julia
In [1]: θ = [0, π/2, π, 3*π/2]
Out[1]: 4-element Vector{Float64}:
 0.0
 1.5707963267948966
 3.141592653589793
 4.71238898038469

In [2]: θ[sortperm(cos.(θ))]
Out[2]: 4-element Vector{Float64}:
 3.141592653589793
 4.71238898038469
 1.5707963267948966
 0.0

In [3]: sort(θ, by = cos)
Out[3]: 4-element Vector{Float64}:
 3.141592653589793
 4.71238898038469
 1.5707963267948966
 0.0

In [4]: sort(θ, by = cos, rev=true)
Out[4]: 4-element Vector{Float64}:
 0.0
 1.5707963267948966
 4.71238898038469
 3.141592653589793
```