---
title: "타입의 계층구조와 메서드 디스패치"
language: ../_language-ko_custom.yml 

number-sections: true
number-depth: 2
crossref:
  chapters: false
---

## 타입의 계층구조 {#sec-type_hierarchy}

Julia 에서 다루는 모든 값은 특정 타입(Type) 의 값입니다. 타입은 julia 언어에서 제공하는 `Int64` 나 `Complex{Float64}` 일 수도 있고 필요에 의해 만든 것일 수도 있습니다. Julia 에서 변수에 값을 할당할 때 타입을 명시적으로 지정할 수 있으며, 명시적으로 지정되어 있지 않을 경우 julia 에 의해 지정되기도 합니다. 다음의 코드와 설명을 보고 이해하시기 바랍니다.

```julia
In [1]: a=3;typeof(a)
Out[1]: Int64

In [2]: b=3.42;typeof(b)
Out[2]: Float64

In [3]: c=Float32(6.44);typeof(c)
Out[3]: Float32

In [4]: d::Float32=3.3;typeof(d)
Out[4]: Float32

In [5]: e::UInt8=3;typeof(e)
Out[5]: UInt8
```

특별히 형을 지정하지 않았을 경우 정수는 `Int64`, 실수는 `Float64` 타입으로 암시적으로 지정됩니다. `c` 의 경우는 `6.44` 라는 `Float64` 의 값을 `Float32` 로 변환시켜 할당하였으며, `d` 의 경우는 변수를 `Float32` 타입으로 지정한 후 대입하여 자동적으로 변환되었습니다. `e` 의 경우도 `UInt8` 타입으로 지정한 후 `3` 을 할당하여 지정된 타입으로 자동적으로 변환되었습니다. 

</br>

### 추상적 타입과 구체적 타입

Julia 의 타입은 **추상적 타입(abstract type)** 과 **구체적 타입(concrete type)** 으로 분류 할 수 있습니다. 구체적 타입은 변수가 지정되어 값을 할당 할 수 있지만 추상적 타입에는 변수를 지정 할 수 없으며, 값도 할당 할 수 없습니다. 억지로 할당할 경우 적당한 구체적 타입으로 변환됩니다. 우리가 지금까지 사용한 `UInt8`, `Int64`, `Float64` 등은 구체적 타입이며 아래의 예에서 `AbstractFloat` 는 부동소수에 대한 추상적인 타입입니다. 

```julia
In [6]: aa=AbstractFloat(0.0);typeof(aa)
Out[6]: Float64
```

어떤 타입이 구체적인 타입인지 추상적인 타입인지는 `isabstracttype()` 함수나 `isconcretetype()` 함수로 확인 할 수 있습니다. Julia 에서 모든 타입은 `Any` 타입에서 뻗어나온 가지와 같은 계층구조를 가지고 있습니다. 예를 들어 julia 에서 제공하는 수 타입은 @fig-type_hierarchy 와 같은 계층구조를 가집니다. 


Julia 에서 추상적 타입은 `abstract` 키워드를 사용하여 다음과 같이 정의합니다.

```julia
abstract type Number end
abstract type Real     <: Number end
abstract type AbstractFloat <: Real end
abstract type Integer  <: Real end
abstract type Signed   <: Integer end
abstract type Unsigned <: Integer end
```

위의 코드는 저자가 임의로 작성한 코드가 아니라 julia 에서 실제 추상 타입을 구현한 코드를 복사한 것입니다. `abstract type Real <: Number end` 에서 `Real <: Number` 는 `Real` 타입이 미리 정의된 `Number` 타입의 하위 타입 이란 의미입니다. `Number` 라는 수 전체를 아우르는 상위 타입이 있으며, 실수를 나타내는 `Real` 타입, 부동소수를 나타내는 `AbstractFloat` 타입, 정수를 나타내는 `Integer` 타입, 부호 있는 정수를 나타내는 `Signed` 타입, 부호 없는 정수, 즉 `0` 을 포함한 자연수를 나타내는 `Unsigned` 타입과 같은 추상 타입이 있습니다. 


Julia 의 모든 타입은 어떤 타입의 하위 타입(subtype) 이며 단 하나의 상위 타입(supertype) 을 계승합니다. 타입의 하위타입에는 갯수 제한이 없습니다. 타입 계층의 최상단에는 `Any` 타입이 있으며, 모든 타입은 `Any` 의 하위타입 이던가 `Any` 의 하위타입의 하위타입 이던가.... `Any` 의 몇대 자손중의 하나입니다. 심지어 `Any` 의 상위타입은 `Any` 입니다. 어떤 타입의 상위 타입을 알기 위해서는 `supertype()` 함수를 사용 하며 하위 타입들을 알기 위해서는 `subtypes()` 함수를 사용합니다.

```julia
In [7]: supertype(UInt8)
Out[7]: Unsigned

In [8]: subtypes(Integer)
Out[8]: 3-element Vector{Any}:
 Bool
 Signed
 Unsigned
```

어떤 타입의 상위 타입은 추상적 타입만이 가능하며, 구체적 타입은 어떤 타입의 상위 타입이 될 수 없습니다. 당연히 `Any` 는 추상적 타입입니다. 그림 @fig-type_hierarchy 은 Julia 에서 자체적으로 제공하는 수(number) 에 대한 타입의 계층 구조입니다. 추상적 타입은 좌우 변이 두겹인 사각형, 구체적 타입은 좌우가 둥근 도형으로 표현하였습니다. `Irrational` 은 $\pi$ 나 $e$ 같은 특별한 무리수를 위한 타입으로 계산 될 때 적당한 타입의 값으로 변환됩니다.

```{mermaid}
%%| label: fig-type_hierarchy
%%| fig-cap: "Type hierarchy of number"
flowchart TB
    Any[[Any]] --- Number[[Number]]
    Number ---- Real[[Real]]
    Number --- Complex[[Complex]]
    Real ------- Integer[[Integer]] 
    Real --- Rational([Rational])
    Real --- AbstractIrrational[[AbstractIrrational]]
    Real ----- AbstractFloat[[AbstractFloat]]
    Integer --- Unsigned[[Unsigned]] & Bool([Bool])
    Integer ----- Signed[[Signed]]
    AbstractIrrational --- Irrational[Irrational]
    AbstractFloat --- Float16([Float16]) & Float32([Float32]) & Float64([Float64])  &  BigFloat([BigFloat])
    Unsigned --- UInt8([UInt8]) & UInt16([UInt16]) & UInt32([UInt32]) & UInt64([UInt64]) & UInt128([UInt128])
    Signed --- Int8([Int8]) & Int16([Int16]) & Int32([Int32]) & Int64([Int64])  & Int128([Int128]) & BigInt([BigInt])
```

</br>

이것을 정리하면 다음과 같습니다.

| | 추상적 타입 | 구체적 타입 |
|:---:|:---:|:---:|
| 하위타입 지정 | o | x |
| 변수 할당 | x | o |

여기서 `Complex` 만 추상적 타입으로 구체적인 하위타입이 존재하지 않습니다. 실제로 Julia 에서 복소수는 복합 타입(composite type) 으로 정의됩니다. 여기에 대해서는 [복합 타입](09_composite_types.qmd)에서 다루겠습니다.

</br>

### 원시 타입 

`Int64` 나 `Float32` 와 같이 값이 저장되는데 사용되는 바이트 수가 결정된 구체적 타입을 **원시 타입(Primitive type)** 이라고 합니다. `isprimitivetype()` 함수를 통해 특정 타입이 원시 타입인지를 확인할 수 있습니다. Julia 의 모든 기본 타입은 julia 자체 내에서 다음과 같이 정의되었습니다. 
[저의 경우는 Julia 설치 디렉토리의 `/share/julia/base/boot.jl` 파일에 정의되었습니다.]{.aside}

```julia
primitive type Float16 <: AbstractFloat 16 end
primitive type Float32 <: AbstractFloat 32 end
primitive type Float64 <: AbstractFloat 64 end

primitive type Bool <: Integer 8 end
primitive type Char <: AbstractChar 32 end

primitive type Int8    <: Signed   8 end
primitive type UInt8   <: Unsigned 8 end
primitive type Int16   <: Signed   16 end
primitive type UInt16  <: Unsigned 16 end
primitive type Int32   <: Signed   32 end
primitive type UInt32  <: Unsigned 32 end
primitive type Int64   <: Signed   64 end
primitive type UInt64  <: Unsigned 64 end
primitive type Int128  <: Signed   128 end
primitive type UInt128 <: Unsigned 128 end
```

첫번째 `primitive type Float16 <: AbstractFloat 16 end` 는 `Float16` 타입은 `AbstractFloat` 타입의 하위타입으로 16비트, 즉 2바이트를 차지하는 타입이라는 의미입니다. 앞서 말했듯이 구체적 타입은 상위 타입이 될 수 없으므로 원시 타입은 어떤 추상 타입의 하위타입이 됩니다.


</br>

### Type Union {#sec-type_union}

예를 들어 당신이 `Int64` 와 `String` 타입만을 가져야 하는 어떤 변수를 원할 때 `Union` 키워드를 이용하여 다음과 같이 새로운 타입을 만들 수 있습니다. 

```julia
In [1]: IntOrString = Union{Int,AbstractString}
Out[1]: Union{Int64, AbstractString}

In [2]: 1 :: IntOrString
Out[2]: 1

In [3]: "Hello!" :: IntOrString
Out[3]: "Hello!"

In [4]: 1.0 :: IntOrString
ERROR: TypeError: in typeassert, expected Union{Int64, AbstractString}, got a value of type Float64
...
```

`Union` 이후의 `{ }` 에는 여러개의 타입이 나열 될 수 있으며, 나열되는 타입은 구체적 타입일 수도 있고, 추상적인 타입일 수도 있습니다. 추상적인 타입일 경우 지정되는 변수에 가장 적당한 하위타입으로 지정됩니다.

</br>

## 메서드 다중 디스패치(Multiple dispatch) {#sec-method_multiple_dispatch}


### 함수와 메서드

각각의 컴퓨터 프로그래밍 언어나 프로그래밍 패러다임마다 함수와 메서드의 관계가 다릅니다. 보통 프로그래밍에서 함수란 어떤 일련의 동작들을 모아 하나의 기능으로 만든 것을 말합니다. Julia 기본 문서의 [Methods](https://docs.julialang.org/en/v1/manual/methods/#Parametric-Methods) 를 보면 Julia 에서 보는 함수와 메서드는 이와는 약간 다릅니다. Julia 에서의 함수는 일종의 기능의 추상화입니다. 예를 들어 두 수를 더하는 함수를 생각해 봅시다. 먼저 원시 타입에 대한 더하기를 생각해보면, 원시 타입이란 특정한 비트의 배열이기 때문에 각 원시 타입마다 내부적으로 더하기를 다르게 구현해야 합니다. 하지만 모두 `+` 라는 연산자에 대해 정의됩니다. 이렇게 추상적으로 정의된 함수에 대해 각 타입마다 구체적으로 구현된 코드를 julia 에서는 메서드라고 부릅니다. [함수와 메서드의 의미와 관계는 언어마다 다르기 때문에 julia 에서 사용되는 용어가 다른 용어에서는 다른 의미 일 수 있습니다.]{.aside}

다음의 함수 정의를 봅시다.

```julia
In [1]: function myfunc1(a, b)
            return a*b
        end
Out[1]: myfunc1 (generic function with 1 method)
```

함수를 정의할 때 특별히 인자에 제한조건을 두지 않았습니다. 이제 인자의 타입을 바꿔 가며 함수를 실행시켜봅시다.

```julia
In [2]: myfunc1(1, 3)
Out[2]: 3

In [3]: myfunc1(2.2, 3.3)
Out[3]: 7.26

In [4]: myfunc1("a", "bc")
Out[4]: "abc"
```

Julia 는 함수의 정의를 기억했다가 처음 실행할 때 주어진 인자의 타입에 맞추어 컴파일하며 한 번 컴파일된 함수는 그 인자 타입으로 다음번 실행 할 때는 컴파일 없이 빠르게 실행 할 수 있습니다다. 

`myfunc1` 함수를 선언 할 때 인자의 타입에 대해 제한을 가하지 않았다는 것은 `a`, `b` 모두 `Any` 타입으로 지정되었다는 것입니다. 만약 `a`, `b` 모두 정수일 때, 즉 `Integer` 의 하위타입일 때는 곱하기가 아니라 더하기를 수행하기를 원한다고 해 봅시다.

```julia
In [5]: function myfunc1(a::Integer, b::Integer)
        return a+b
        end
Out[5]: myfunc1 (generic function with 2 methods)

In [6]: myfunc1(1, 2)
Out[6]: 3

In [7]: myfunc1(1.0, 2.0)
Out[7]: 2.0

In [8]: myfunc1(1.0, 2)
Out[8]: 2.0
```

인자가 둘 다 정수일 때는 더하기가, 둘 중 하나라도 정수가 아니라면 곱하기가 수행됩니다. 만약 `a` 가 문자열이고 `b` 가 정수라면 `a` 를 `b` 의 절대값만큼 반복하도록 합시다.


```julia
In [9]: function myfunc1(a::String, b::Integer) 
        return a^abs(b)
        end
Out[9]: myfunc1 (generic function with 3 methods)

In [10]: myfunc1("aa", -3)
Out[10]: "aaaaaa"
```

이렇게 우리는 인자 타입에 대해 `myfunc1` 이라는 함수 이름으로 3개의 메서드를 생성하였습니다. 

</br>

#### 시그너쳐와 `methods` 함수 {#sec-signature_of_methods}

메서드의 시그너쳐(signature) 는 함수이름과 인자, 그리고 인자의 타입을 포함합니다. 하나의 함수의 다양한 메서드는 시그너쳐로 구분되며, 한 함수의 시그너쳐는 `method` 함수로 확인 할 수 있습니다. `methods(myfunc1)` 는 함수 `myfunc1` 에 대한 모든 메서드의 시그너쳐를 반환합니다. 

```julia
In [12]: methods(myfunc1)
Out[12]: # 3 methods for generic function "myfunc1" from Main:
 [1] myfunc1(a::Integer, b::Integer)
     @ REPL[5]:1
 [2] myfunc1(a::String, b::Integer)
     @ REPL[9]:1
 [3] myfunc1(a, b)
     @ REPL[1]:1
```

여기서 볼 수 있듯이 `myfunc1` 함수에는 세가지의 메서드가 구현되었으며, 특정 타입에 대해 어떤 메서드가 실행될 지 파악 할 수 있습니다.

</br>

함수 뿐만 아니라 연산자도 여러개의 메서드를 가질 수 있습니다. `methods(+)` 를 통해 `+` 연산자가 아주 많이 정의되었다는 것을 확인해 보시기 바랍니다. 이렇게 같은 함수나 연산자의 이름으로 인자의 타입에 따라 기능을 정하는 것을 메서드 디스패치, 혹은 다중 디스패치라고 합니다. 여러분은 앞으로 새로운 타입을 만드는 방법을 알게 될텐데 이미 존재하는 `+`, `-` 등의 연산자나 `println`, `length` 등의 함수를 다중 디스패치를 통해 타입에 맞게 사용 할 수 있습니다.

</br>

### 모호한 메써드 {#sec-method_ambiguity}

$a^b$ 를 리턴하는 `mypow` 함수를 다음과 같이 구현했다고 해 봅시다.

```julia
In [1]: function mypow(a, b)
        return a^b
        end
Out[1]: mypow (generic function with 1 method)

In [2]: function mypow(a::Number, b)
        return a^b
        end
Out[2]: mypow (generic function with 2 methods)

In [3]: function mypow(a, b::Number)
        return a^b
        end
Out[3]: mypow (generic function with 3 methods)

In [4]: methods(mypow)
Out[4]: # 3 methods for generic function "mypow" from Main:
 [1] mypow(a::Number, b)
     @ REPL[2]:1
 [2] mypow(a, b::Number)
     @ REPL[3]:1
 [3] mypow(a, b)
     @ REPL[1]:1
```

3가지의 메서드가 구현되었습니다. 여기에 `mypow(2, 3)` 는 어떤 값을 리턴할까요? 컴파일러 입장에서는 두번째와 세번째 메써드가 동등한 권리를 갖고 있기 때문에 불확실 성이 존재하며 컴파일러는 에러를 발생시킵니다. 

```julia
In [5]: mypow(2, 3)
ERROR: MethodError: mypow(::Int64, ::Int64) is ambiguous.
```

</br>

### 타입 매개변수를 갖는 메서드 {#sec-parametric_methods}

메서드를 정의할 때 구체적, 추상적인 타입을 직접 표현하는 것이 아니라 타입 매개변수를 이용하여 정의 할 수도 있습니다. Julia 공식 문서의 예를 가져와 보겠습니다. 함수 인자 두개에 대해 그 타입이 같으면 `true`, 다르면 `false` 를 반환하는 함수를 작성해 봅시다. `typeof` 함수를 이용 할 수도 있겠지만 메서드를 이용하여 깔끔하게 정의 할 수도 있습니다. 일단 두개의 타입이 같으면 `true` 를 반환하는 함수를 작성합니다.

```julia
In [1]: same_type(x::T, y::T) where {T} = true
Out[1]: same_type (generic function with 1 method)
```

여기서 `T` 는 타입 매개변수 입니다. 함수의 인자 표현에서 `x::T, y::T` 는 `x` 와 `y` 가 그것이 무엇이든 같은 타입이어야 한다는 의미입니다. 타입이 다르면 적용할 메서드가 없다는 에러가 발생합니다.

```julia
In [2]: same_type(1, 2.0)
ERROR: MethodError: no method matching same_type(::Int64, ::Float64)
```

타입이 다를 때는 `false` 를 반환하게 하려면

```julia
In [3]: same_type(x,y) = false
Out[3]: same_type (generic function with 2 methods)
```

와 같이만 하면 됩니다. 

```julia
In [4]: same_type(1, 2.0)
Out[4]: false
```

이것이 작동하는 원리는 쉽습니다. 함수를 호출하면 메서드의 시그너쳐를 확인하여 실제 실행될 메서드를 찾는데 두 인자가 같은 타입일 경우 `same_type(x::T, y::T) where {T} = true` 메서드가 호출되며, 다른 타입일 경우 `same_type(x,y) = false` 메서드가 호출됩니다. 

</br>

#### 함수 안에서의 타입매개변수

시그너쳐에 정해진 타입매개변수는 함수 안에서도 사용 될 수 있습니다. 우리는 기본타입의 경우 타입 이름이 함수로 사용 될 수 있다는 것을 앞에서 보았습니다. 예를 들어 `Float64(3)` 에서 `Float64` 는 `Int64` 타입의 `3` 을 `Float64` 타입으로 변환시키는 함수로 사용되었습니다. 다음 함수를 봅시다.

```julia
In [1]: function myconvert(a::T, b) where {T}
        return T(b)
        end
Out[1]: myconvert (generic function with 1 method)

In [2]: myconvert(2.0, 4)
Out[2]: 4.0
```

이 `myconvert` 함수는 두개의 인자를 받아 두번째 인자를 첫번째 인자의 타입으로 변환시켜 반환하는 함수입니다. 이 때 함수 시그니쳐의 타입 매개변수 `T` 가 함수의 몸체에서 사용되었습니다. 

</br>

#### 여러개의 타입매개변수 사용과 타입 제한

함수 시그너쳐의 타입 매개변수는 여러개 일 수 있으며, 특정 타입 혹은 특정 타입의 하위타입이 되도록 제한을 걸 수 있습니다. 예를 들어 봅시다.

```julia
In [1]: function myconvert2(a::T, b::N) where {T<:Number, N<:Integer}
        return T(b)
        end
Out[1]: myconvert2 (generic function with 1 method)

In [2]: myconvert2(4.0, 3)
Out[2]: 3.0

In [3]: myconvert2(4.0, 3.0)
ERROR: MethodError: no method matching myconvert2(::Float64, ::Float64)
```

`myconvert2` 함수는 `T` 와 `N` 의 두개의 타입매개변수를 가지며 `T` 는 `Number` 의 하위타입, `N` 은 `Integer` 의 하위타입이어야 합니다. `In [3]` 와 같이 두번째 인자가 정수가 아닐 경우에는 적합한 메서드를 찾을 수 없으므로 `MethodError` 가 발생합니다.


</br>

#### 아무것도 아닌 함수

여러 메서드를 가지는 함수를 작성할 때 일단은 이름을 갖고 코드가 에러를 발생시키지는 않지만 실행은 안되는 함수가 필요할 때가 있습니다. 실용적으로 필요한 것이 아니라 문서화 작업 혹은 앞으로 작성할 함수라고 흔적을 남기기 위해 사용되기도 합니다. 


```julia
function great_function_with_many_methods end
```

`great_function_with_many_methods` 은 함수로서 이름공간 포함되지만 아무 일도 할 수 없습니다. 